;;; text-moving.el -- Set of useful text manipulation shortcuts.
;;

;;; Commentary:

;; So far, mode provides two interactive functions:
;;
;; `move-text-down` which swaps current line with the one below.
;; `move-text-up` which swaps current line with the one above.
;;

;;; Code:

(defun move-text-internal (arg)
  "Move selected text"
  (cond
   ((and mark-active transient-mark-mode)
    (if (> (point) (mark))
        (exchange-point-and-mark))
    (let ((column (current-column))
          (text (delete-and-extract-region (point) (mark))))
      (forward-line arg)
      (move-to-column column t)
      (set-mark (point))
      (insert text)
      (exchange-point-and-mark)
      (setq deactivate-mark nil)))
   (t
    (let ((column (current-column)))
      (beginning-of-line)
      (when (or (> arg 0) (not (bobp)))
        (forward-line)
        (when (or (< arg 0) (not (eobp)))
          (transpose-lines arg))
        (forward-line -1))
      (move-to-column column t)))))

(defun move-text-down (arg)
  "Move selected text or line down"
  (interactive "*p")
  (move-text-internal arg))

(defun move-text-up (arg)
  "Move selected text or line up"
  (interactive "*p")
  (move-text-internal (- arg)))

(provide 'text-moving)